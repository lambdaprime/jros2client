plugins {
  id 'com.diffplug.spotless'
  id 'java'
}

apply plugin : 'eclipse'

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to classpath
  whenMerged {
    entries.findAll { it.kind == 'lib' || it.kind == 'src' }.each { it.entryAttributes['module'] = 'false' }
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  // takes latest release version
  implementation "io.github.lambdaprime:jros2client:+"
  implementation "io.opentelemetry:opentelemetry-api:1.43.0"
  implementation "io.github.lambdaprime:id.opentelemetry-exporters-pack:4.0"
}

sourceCompatibility = 17
targetCompatibility = 17

jar {
  archiveFileName = "diagnostics_otel.jar"
  manifest { 
    attributes "Implementation-Version": "${rootProject.version}",
      "Main-Class": "id.jros2client.examples.diagnostics_otel.DiagnosticsOpenTelemetryApp"
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

// run spotless and format code before the build
classes.dependsOn spotlessApply
